#!/usr/bin/perl
# csv2xml is (C) 2010 by Jan-Piet Mens
# The contents of this file are subject to the Mozilla Public License Version
# 1.1 (the "License"); you may not use this file except in compliance with
# the License. You may obtain a copy of the License at
# http://www.mozilla.org/MPL/
# 
# Software distributed under the License is distributed on an "AS IS" basis,
# WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
# for the specific language governing rights and limitations under the
# License.

use strict;
use XML::Writer;
use IO::File;
use Encode;

my $output = new IO::File("> phonebook.xml") or die "Can't creat output: $!";
my $xml = new XML::Writer(OUTPUT => $output, NEWLINES => 0);

$xml->xmlDecl( "iso-8859-1" );
$xml->startTag('phonebooks');
$xml->startTag('phonebook');

while (<>) {
	s/\s+$//g;
	next if (/^#/ || /^$/);

	Encode::from_to($_, 'utf8', 'iso-8859-1');
	my ($name, $home, $work, $mobile, $quick, $main) = split(/;/, $_, 6);

	my $tels = '';

	$xml->startTag('contact');

	  $xml->startTag('person');
	    $xml->dataElement('realName', $name);
	  $xml->endTag('person');

	$xml->startTag('category');
	$xml->endTag('category');

	$xml->startTag('services');
	$xml->endTag('services');
	$xml->startTag('setup');
	$xml->endTag('setup');

	$xml->startTag('telephony');

		addphone('home', $home, $quick, $main) if ($home);
		addphone('work', $work, $quick, $main) if ($work);
		addphone('mobile', $mobile, $quick, $main) if ($mobile);

	$xml->endTag('telephony');

	$xml->endTag('contact');
}

$xml->endTag('phonebook');
$xml->endTag('phonebooks');
$xml->end();
$output->close();

sub addphone {
	my ($type, $number, $quickdial, $main) = @_;

	my $prio = ($type eq $main) ? '1' : '';

	$quickdial = ($quickdial && $type eq $main) ? $quickdial : '';
	
	$xml->dataElement('number', $number,
		'type' => $type,
		'prio' => $prio,
		'quickdial' => $quickdial);
}
